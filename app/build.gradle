apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.ruslanlyalko.wl"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def propsFile = file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile file('keystore/keystore0.jks')
                keyAlias = props['keyAlias']
                storePassword = props['storePassword']
                keyPassword = props['keyPassword']
            }
        }
        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date()
            def formattedDate = date.format('dd-MM-yy')
            outputFileName = "WL_${variant.versionName}_${variant.name}_${formattedDate}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
configurations {
    cleanedAnnotations
    compile.exclude group: 'org.jetbrains', module: 'annotations'
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation('com.google.firebase:firebase-core:17.0.1')
    implementation('com.google.firebase:firebase-auth:18.1.0')
    implementation('com.google.firebase:firebase-firestore:20.2.0')
    implementation('com.google.firebase:firebase-database:18.0.1')
    implementation('com.google.firebase:firebase-functions:18.1.0')
    implementation('com.google.firebase:firebase-messaging:19.0.1')
    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // ButterKnife
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    // Calligraphy
    implementation "uk.co.chrisjenx:calligraphy:2.3.0"
    // Time Picker
    implementation("com.wdullaer:materialdatetimepicker:3.3.1") {
        exclude group: 'com.android.support'
    }
    // Calendar with events
    implementation("com.github.sundeepk:compact-calendar-view:2.0.2.1") {
        exclude group: 'com.android.support'
    }
    //Excel
    implementation('org.apache.poi:poi:3.9') {
        exclude group: 'com.android.support'
    }
    // Rx Permission
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    // Overscroll
    implementation 'me.everything:overscroll-decor-android:1.0.4'
    // Flow layout for Project Titles
    implementation "com.wefika:flowlayout:0.4.1"
    // Snow effect
    implementation 'com.github.jetradarmobile:android-snowfall:1.2.0'
    //rx
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxjava:2.2.0"
    // Fast scroll
    implementation 'com.futuremind.recyclerfastscroll:fastscroll:0.2.5'
    // Time Wheel
    implementation 'com.yarolegovich:discrete-scrollview:1.4.9'
    // Gson for exporting
    implementation 'com.google.code.gson:gson:2.8.5'
    // Adapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
}
